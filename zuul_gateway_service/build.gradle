buildscript {
	ext {
		springBootVersion = '2.0.5.RELEASE'
	}
	repositories {
	    jcenter()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1")
        classpath("io.github.swagger2markup:swagger2markup-spring-restdocs-ext:1.0.0")
        classpath("io.github.swagger2markup:swagger2markup-gradle-plugin:1.1.0")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'io.github.swagger2markup'

jar{
	baseName = 'zuul_gateway_service'
}
version = '0.0.1-SNAPSHOT'
gradle.ext.imageVersion="4.14"
gradle.ext.dockerRepository="ecmregistry.azurecr.io"
gradle.ext.buildVMIP="192.168.52.207"
sourceCompatibility = 1.8

repositories {
	jcenter()
	mavenCentral()
}

ext{
  asciiDocOutputDir = file("${buildDir}/asciidoc/generated")
  swaggerOutputDir = file("${buildDir}/swagger")
  snippetsOutputDir = file("${buildDir}/asciidoc/snippets")
  springfoxVersion = '2.6.1'
  springCloudVersion = 'Finchley.SR1'
}

dependencies {
	//compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.2.3.RELEASE'
	//compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.2.3.RELEASE'
	//compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
	//implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
	implementation('org.springframework.cloud:spring-cloud-starter-netflix-zuul')
	compile group:'org.springframework.boot' , name:'spring-boot-starter-web'
	compile group:'org.springframework.boot' , name:'spring-boot-devtools'
	compile group:'org.springframework.boot' , name:'spring-boot-starter-actuator'
	compile group:'io.springfox' , name:'springfox-swagger2' , version: '2.8.0'
	compile group:'io.springfox' , name:'springfox-swagger-ui', version: '2.8.0'
	compile group: 'org.json', name: 'json', version: '20180813'
	compile group: 'com.google.code.gson', name: 'gson'
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}


group = gradle.dockerRepository

//**** Start of Docker operations *****
def imageName = "${project.group}/frog.ecm.gateway_server/${jar.baseName}:${gradle.ext.imageVersion}"

import org.apache.tools.ant.filters.ReplaceTokens
task copyDockerfile(type: Copy) {
    dependsOn "build"
    from "src/main/docker"
    into "$buildDir/libs"
    filter(ReplaceTokens, tokens: [
        'DOCKER_REPOSITORY': gradle.dockerRepository
    ])
}

task buildDockerImage (type: Exec) {
    dependsOn copyDockerfile
    commandLine "docker", "build", "-t",imageName , "$buildDir/libs" 
}

task pushDockerImage (type: Exec) {
    dependsOn buildDockerImage
    commandLine "docker", "push", imageName
}
//**** End of Docker operations *****
    
test {
	exclude '**/RestDocTest.class'
}

// Automation for documentation using swagger and asciidoctor
task documentTask(type: Test) {	
     doFirst{
       println("Running documentTask")
     }
     systemProperty 'io.springfox.staticdocs.outputDir', swaggerOutputDir
     systemProperty 'io.springfox.staticdocs.snippetsOutputDir', snippetsOutputDir
}

convertSwagger2markup {
    dependsOn documentTask
    swaggerInput "${swaggerOutputDir}/swagger.json"
    outputDir asciiDocOutputDir
    config = [
            'swagger2markup.pathsGroupedBy' : 'TAGS',
            'swagger2markup.extensions.springRestDocs.snippetBaseUri': snippetsOutputDir.getAbsolutePath()]
}

asciidoctor {
    dependsOn convertSwagger2markup
    sources {
        include 'index.adoc'
    }
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '3',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            hardbreaks: '',
            generated: asciiDocOutputDir
    ]
}

configure(buildDockerImage) {   
    group = 'Docker'
    description = 'build jar and package to docker image'
}

configure(pushDockerImage) {   
    group = 'Docker'
    description = 'push docker image to dockerHost.newgen.co.in'
}